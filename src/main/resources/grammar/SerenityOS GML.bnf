{
    parserClass="me.mattco.serenityos.gml.GMLParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="GML"
    psiImplClassSuffix="Impl"
    psiPackage="me.mattco.serenityos.gml.psi.api"
    psiImplPackage="me.mattco.serenityos.gml.psi.impl"

    elementTypeHolderClass="me.mattco.serenityos.gml.GMLTypes"
    elementTypeClass="me.mattco.serenityos.gml.GMLElementType"
    tokenTypeClass="me.mattco.serenityos.gml.GMLToken"

    tokens=[
        COMMENT="regexp:(//[^\r\n]*)+"
    ]

    implements(".*")="me.mattco.serenityos.gml.psi.GMLPsiElement"
    generateTokenAccessors=true
}

File ::= Component

Component ::= ComponentName OPEN_CURLY ComponentBody? CLOSE_CURLY {
    implements="me.mattco.serenityos.gml.psi.interfaces.IGMLComponent"
    mixin="me.mattco.serenityos.gml.psi.mixins.GMLComponentMixin"
    pin=1
    recoverWhile=Component_recover
}

private Component_recover ::= !(AT | CLOSE_CURLY | IDENTIFIER)

ComponentName ::= AT NAMESPACE? IDENTIFIER (NAMESPACE IDENTIFIER)* {
    name="component name"
    pin=1
}

ComponentBody ::= (Property | Component)+
Property ::= PropertyIdentifier COLON Value {
    implements="me.mattco.serenityos.gml.psi.interfaces.IGMLProperty"
    mixin="me.mattco.serenityos.gml.psi.mixins.GMLPropertyMixin"
    pin=1
    recoverWhile=Property_recover
}
private Property_recover ::= !(IDENTIFIER | CLOSE_CURLY | AT)

PropertyIdentifier ::= IDENTIFIER
Value ::=
    BOOLEAN
  | NUMBER
  | STRING
  | Array
  | Component

Array ::= OPEN_BRACKET [Value (COMMA Value)*] CLOSE_BRACKET
